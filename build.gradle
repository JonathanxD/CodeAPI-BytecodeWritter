buildscript {
    apply from: file('commons/versions.gradle')

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:$license_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }

}

group 'com.koresframework'
version '4.2.16.bytecode'

apply from: file("commons/common.gradle")

// Publish
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
    maven {
        name = "JGang"
        url "https://gitlab.com/api/v4/projects/30392813/packages/maven"
    }
}

dependencies {
    implementation "com.koresframework:kores:4.2.16.base"
    implementation 'com.github.jonathanxd:bytecode-disassembler:2.4.2'
    implementation "org.ow2.asm:asm:9.2"
    implementation "org.ow2.asm:asm-analysis:9.2"
    implementation "org.ow2.asm:asm-tree:9.2"
    implementation "org.ow2.asm:asm-util:9.2"

    testImplementation "com.github.jonathanxd:links:$iutils_version"
    testImplementation "com.koresframework:kores-test:4.2.16.base"
}

tasks.dokkaGfm.configure {
    enabled = false
}

allprojects {
    tasks.whenTaskAdded { task ->
        if (task.name.contains("dokkaGfm")) {
            task.enabled = false
        }
    }
}

shadowJar {
    dependencies {
        include(dependency("org.ow2.asm:asm-all"))
        include(dependency("com.koresframework:kores:4.2.4.base"))
        include(dependency("com.github.jonathanxd:bytecode-disassembler"))
        include(dependency("com.github.jonathanxd:jwiutils"))
    }
}

publishing {
    repositories {
        maven {
            name = "Local"
            // change to point to your repo, e.g. http://my.org/repo
            url = "$buildDir/repo"
        }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/koresframework/kores-bytecodewriter"
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
        maven {
            name = "GitLab"
            url "https://gitlab.com/api/v4/projects/28905266/packages/maven"
            credentials(HttpHeaderCredentials) {
                def ciToken = System.getenv("CI_JOB_TOKEN")
                if (ciToken != null && !ciToken.isEmpty()) {
                    name = "Job-Token"
                    value = System.getenv("CI_JOB_TOKEN")
                } else {
                    name = "Private-Token"
                    value = project.findProperty("GITLAB_TOKEN") ?: System.getenv("GITLAB_TOKEN")
                }
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
        maven {
            name = "GitLabJgang"
            url "https://gitlab.com/api/v4/projects/30392813/packages/maven"
            credentials(HttpHeaderCredentials) {
                def ciToken = System.getenv("CI_JOB_TOKEN")
                if (ciToken != null && !ciToken.isEmpty()) {
                    name = "Job-Token"
                    value = System.getenv("CI_JOB_TOKEN")
                } else {
                    name = "Private-Token"
                    value = project.findProperty("GITLAB_TOKEN") ?: System.getenv("GITLAB_TOKEN")
                }
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }

    publications {
        maven(MavenPublication) {
            artifactId 'kores-bytecodewriter'
            from components.kotlin

            artifact sourcesJar
        }
    }
}